rule assemble_megahit:
    """
    Run Megahit assembly on fastq
    """
    input:
        forward = qc_dir + "{sample}/filtered/{sample}.R1.trimmed.filtered.fastq.gz",
        reverse = qc_dir + "{sample}/filtered/{sample}.R2.trimmed.filtered.fastq.gz"
    output:
        assemble_dir + "{sample}/megahit/{sample}.contigs.fa"
    params:
        mem_b = 120000000000,
        out_dir = os.path.dirname(output[0]),
        temp_dir = lambda wildcards: os.path.join(config['tmp_dir_root'],
                                                 'tmp_%s' % wildcards.sample)
    threads:
        12
    log:
        assemble_dir + "logs/assemble_megahit.sample_{sample}.log"
    benchmark:
        "benchmarks/assemble/assemble_megahit.sample_{sample}.txt"
    conda:
        "../envs/env_assemble.yaml"
    shell:
        """
        megahit -1 {input.forward} -2 {input.reverse} \
        -m {params.mem_b} -t {threads} -o {params.temp_dir}/megahit \
        --out-prefix {wildcards.sample} \
        2> {log} 1>&2

        scp {params.temp_dir}/megahit/{{*.log,*.fa,*.txt}} {params.out_dir}/.
        """

rule assemble_metaspades:
    input:
        forward = qc_dir + "{sample}/filtered/{sample}.R1.trimmed.filtered.fastq.gz",
        reverse = qc_dir + "{sample}/filtered/{sample}.R2.trimmed.filtered.fastq.gz"
    output:
        assemble_dir + "{sample}/metaspades/contigs.fasta"
    log:
        assemble_dir + "logs/assemble_metaspades.sample_{sample}.log"
    benchmark:
        "benchmarks/assemble/assemble_metaspades.sample_{sample}.txt"
    conda:
        "../envs/env_assemble.yaml"
    params:
        mem=240,
        out_dir = os.path.dirname(output[0]),
        temp_dir = lambda wildcards: os.path.join(config['tmp_dir_root'],
                                                 'tmp_%s' % wildcards.sample)
    threads:
        12
    shell:
        """
        set +u; {params.env}; set -u

        spades.py --meta -t {threads} -m {params.mem} \
        -1 {input.forward} -2 {input.reverse} -o {params.temp_dir} \
        2> {log} 1>&2

        scp -r {params.temp_dir}/{{spades.log,*.fasta,assembly_graph*,*.paths}} {params.out_dir}/.
        """

rule assemble:
    input:
        expand(rules.assemble_megahit.output, sample=samples),
        expand(rules.assemble_metaspades.output, sample=samples)
