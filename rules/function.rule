
rule function_humann2:
    """
    Runs HUMAnN2 pipeline using general defaults.

    Other HUMAnN2 parameters can be specified as a quoted string in
    params: humann2: other.
    """
    input:
        forward = qc_dir + "{sample}/filtered/{sample}.R1.trimmed.filtered.fastq.gz",
        reverse = qc_dir + "{sample}/filtered/{sample}.R2.trimmed.filtered.fastq.gz",
        metaphlan_in = func_dir + "metaphlan2/joined_taxonomic_profile_max.tsv"
    output:
        genefamilies = func_dir + "{sample}/humann2/{sample}_genefamilies.tsv",
        pathcoverage = func_dir + "{sample}/humann2/{sample}_pathcoverage.tsv",
        pathabundance = func_dir + "{sample}/humann2/{sample}_pathabundance.tsv"
    params:
        nt_db = os.path.join(config['params']['db_dir'],
                            config['params']['humann2']["nt_db"]),
        aa_db = os.path.join(config['params']['db_dir'],
                             config['params']['humann2']["aa_db"]),
        mp2 = os.path.join(config['params']['db_dir'],
                          config['params']['metaphlan2']['db']),
        other = config['params']['humann2']['other'],
        temp_dir = lambda wildcards: os.path.join(config['tmp_dir_root'],
                                                 'tmp_%s' % wildcards.sample,
                                                 'function_humann2')
    threads:
        8
    log:
        func_dir + "logs/function_humann2.sample_{sample}.log"
    benchmark:
        "benchmarks/function/function_humann2.sample_{sample}.json"
    conda:
        "../envs/env_humann2.yaml"
    shell:
        """
        mkdir -p {params.temp_dir}
        zcat {input.forward} {input.reverse} > {params.temp_dir}/input.fastq

        humann2 --input {params.temp_dir}/input.fastq \
        --output {params.temp_dir}/{wildcards.sample} \
        --output-basename {wildcards.sample} \
        --nucleotide-database {params.nt_db} \
        --protein-database {params.aa_db} \
        --taxonomic-profile {input.metaphlan_in} \
        --o-log {log} \
        --threads {threads} \
        {params.other} 2> {log} 1>&2

        scp {params.temp_dir}/{wildcards.sample}/{wildcards.sample}_genefamilies.tsv {output.genefamilies}
        scp {params.temp_dir}/{wildcards.sample}/{wildcards.sample}_pathcoverage.tsv {output.pathcoverage}
        scp {params.temp_dir}/{wildcards.sample}/{wildcards.sample}_pathabundance.tsv {output.pathabundance}
        
        rm -r {params.temp_dir}
        """

rule humann2:
    input:
        genef = lambda wildcards: expand(func_dir + "{sample}/humann2/{sample}_genefamilies.tsv",
                                         sample=samples),
        pathc = lambda wildcards: expand(func_dir + "{sample}/humann2/{sample}_pathcoverage.tsv",
                                         sample=samples),
        patha = lambda wildcards: expand(func_dir + "{sample}/humann2/{sample}_pathabundance.tsv",
                                         sample=samples)


rule function:
    input:
        rules.humann2.input
